<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN" "DTD/xhtml1-strict.dtd">
<html>
<!-- Copied from https://htdp.org/2003-09-26/Solutions/scheme-eval-def.html by Cyotek WebCopy 1.8.0.652, 01 July 2020, 17:43:44 -->
<head>
<title>Exercise 17.7.4; How to Design Programs </title>
</head>
<body>
<a href="scheme-eval-def1.html">17.7.3</a> | <a href="../Book/curriculum-Z-H-22.html#node_thm_17.7.4">Problem Statement</a>
 | <a href="contents.html">Table of Contents</a>
 | <a href="list-4.html">17.8.1</a><pre>#|

A scheme-expression is either:

  - number,
  - symbol,
  - (make-add scheme-expression scheme-expression)
  - (make-mul scheme-expression scheme-expression)
  - (make-app symbol scheme-expression)
|#

(define-struct add (lhs rhs))
(define-struct mul (lhs rhs))
(define-struct app (rator rand))

;; EXAMPLES

;; (f (+ 1 1))
(make-app 'f (make-add 1 1))

;; (* 3 (g 2))
(make-mul 3 (make-app 'g 2))

; -------------------------------------------------------------------------

#|

A scheme-def is:
  (make-def symbol symbol scheme-expr)

|#

(define-struct def (name arg body))

;; 1. (define (f x) (+ 3 x)) 
(make-def 'f 'x (make-add 3 'x))

;; 2. (define (g x) (* 3 x)) 
(make-def 'g 'x (make-mul 3 'x))

;; 3. (define (h u) (f (* 2 u))) 
(make-def 'h 'u (make-app 'f (make-mul 2 'u)))

;; 4. (define (i v) (+ (* v v) (* v v))) 
(make-def 'i 'v (make-add (make-mul 'v 'v) (make-mul 'v 'v)))

;; 5. (define (k w) (* (h w) (i w))) 
(make-def 'k 'w (make-mul (make-app 'h 'w) (make-app 'i 'w)))

;; (define (f2c f) (* 5/9 (+ f -32)))
(make-def 'f2c 'f (make-mul 5/9 (make-add 'f -32)))

;; (define (circle-area r) (* 3.1415926535 (* r r)))
(make-def 'circle-area 'r (make-mul 3.1415926535 (make-mul 'r 'r)))

; -------------------------------------------------------------------------

;; evaluate-with-defs : scheme-expression list-of-scheme-def -&gt; number
;; evaluates a program with a single definition
(define (evaluate-with-defs a-se defs)
  (cond
    [(number? a-se) a-se]
    [(symbol? a-se) (error 'evaluate-expression "got a variable")]
    [(add? a-se) (+ (evaluate-with-defs (add-lhs a-se) defs)
                    (evaluate-with-defs (add-rhs a-se) defs))]
    [(mul? a-se) (* (evaluate-with-defs (mul-lhs a-se) defs)
                    (evaluate-with-defs (mul-rhs a-se) defs))]
    [(app? a-se) 
     (let ([the-def (lookup-def defs (app-rator a-se))])
       (evaluate-with-defs
        (subst (def-body the-def)
               (def-arg the-def)
               (evaluate-with-defs (app-rand a-se) defs))
        defs))]))

;; subst : scheme-expression symbol number -&gt; scheme-expression
;; replaces all occurrences of var with num in a-se
(define (subst a-se var num)
  (cond
    [(number? a-se) a-se]
    [(symbol? a-se) (if (eq? var a-se)
                        num
                        a-se)]
    [(add? a-se) (make-add (subst (add-lhs a-se) var num)
                           (subst (add-rhs a-se) var num))]
    [(mul? a-se) (make-mul (subst (mul-lhs a-se) var num)
                           (subst (mul-rhs a-se) var num))]
    [(app? a-se) (make-app (app-rator a-se)
                           (subst (app-rand a-se) var num))]))

;; lookup-def : list-of-definitions symbol -&gt; definition
;; finds the definition named `name', and raises an
;; error if that definition isn't present.
(define (lookup-def defs name)
  (cond
    [(empty? defs) (error 'lookup-def "no such definition")]
    [else (cond
            [(symbol=? (def-name (first defs)) name)
             (first defs)]
            [else (lookup-def (rest defs) name)])]))

;; EXAMPLES AS TESTS
(define defs
  (list
   (make-def 'f 'x (make-add 3 'x))
   (make-def 'g 'x (make-mul 3 'x))
   (make-def 'h 'u (make-app 'f (make-mul 2 'u)))
   (make-def 'i 'v (make-add (make-mul 'v 'v) (make-mul 'v 'v)))
   (make-def 'k 'w (make-mul (make-app 'h 'w) (make-app 'i 'w)))
   (make-def 'f2c 'f (make-mul 5/9 (make-add 'f -32)))
   (make-def 'circle-area 'r (make-mul 3.1415926535 (make-mul 'r 'r)))))


(lookup-def defs 'f)
"should be"
(make-def 'f 'x (make-add 3 'x))

(lookup-def defs 'i)
"should be"
(make-def 'i 'v (make-add (make-mul 'v 'v) (make-mul 'v 'v)))

; (lookup-def defs 'len)
; error!

(define f2c-def (make-def 'f2c 'f (make-mul 5/9 (make-add 'f -32))))

(evaluate-with-defs (make-app 'f2c 32) defs)
"should be"
0

(evaluate-with-defs (make-app 'f2c 212) defs)
"should be"
100

(evaluate-with-defs (make-app 'f2c -40) defs)
"should be"
-40

(evaluate-with-defs (make-app 'g (make-add 2 2)) defs)
"should be"
12

(evaluate-with-defs (make-app 'i 2) defs)
"should be"
8

(evaluate-with-defs (make-app 'h 3) defs)
"should be"
9

(evaluate-with-defs (make-add 17 (make-app 'h 4)) defs)
"should be"
28

(evaluate-with-defs (make-app 'k 3) defs)
"should be"
162


; (evaluate-with-defs (make-app 'radius 3) defs)
; error!
</pre>
</body>
<!-- Copied from https://htdp.org/2003-09-26/Solutions/scheme-eval-def.html by Cyotek WebCopy 1.8.0.652, 01 July 2020, 17:43:44 -->
</html>
