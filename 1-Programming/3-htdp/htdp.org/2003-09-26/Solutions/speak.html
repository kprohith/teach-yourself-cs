<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN" "DTD/xhtml1-strict.dtd">
<html>
<!-- Copied from https://htdp.org/2003-09-26/Solutions/speak.html by Cyotek WebCopy 1.8.0.652, 01 July 2020, 17:43:44 -->
<head>
<title>Exercise 10.1.9; How to Design Programs </title>
</head>
<body>
<a href="quad3.html">10.1.8</a> | <a href="../Book/curriculum-Z-H-14.html#node_thm_10.1.9">Problem Statement</a>
 | <a href="contents.html">Table of Contents</a>
 | <a href="contains-dollp.html">10.2.1</a><pre>;; Language: Beginning Student

;; controller : number -&gt; loc
;; The function consumes a number and produces a list
;; with the following 5 items:
;; the dollar amount;
;; the symbol 'dollar if the dollar amount is 1 and 'dollars otherwise;
;; the symbol 'and;
;; the cent amount; and
;; the symbol 'cent if the cent amount is 1 and 'cents otherwise.
(define (controller cents)
  (combine (one-amount (quotient cents 100) 'dollar 'dollars)
           (one-amount (modulo cents 100) 'cent 'cents)))

;; combine : word-list word-list -&gt; word-list
;; combines the dollar and cents words together,
;; including 'and' as appropriate
(define (combine dollar-words cents-words)
  (cond
    [(empty? dollar-words) cents-words]
    [(empty? cents-words) dollar-words]
    [else (append dollar-words (list 'and) cents-words)]))

;; one-amount : number symbol symbol -&gt; word-list
;; computes the words to speak for the amount 'n', given
;; the singular and plural form of the description of the amount
(define (one-amount n singular-form plural-form)
  (cond
    [(= n 0) empty]
    [(= n 1) (list 1 singular-form)]
    [else (list n plural-form)]))

;; Testcases

(check-expect (one-amount 0 'cent 'cents) empty)
(check-expect (one-amount 1 'cent 'cents) (list 1 'cent))
(check-expect (one-amount 2 'cent 'cents) (list 2 'cents))

(check-expect (controller   0)  (list))
(check-expect (controller   1)  (list 1 'cent))
(check-expect (controller   50) (list 50 'cents))
(check-expect (controller  100) (list 1 'dollar))
(check-expect (controller  101) (list 1 'dollar 'and 1 'cent))
(check-expect (controller  103) (list 1 'dollar 'and 3 'cents))
(check-expect (controller 1101) (list 11 'dollars 'and 1 'cent))
(check-expect (controller 1102) (list 11 'dollars 'and 2 'cents))
</pre>
</body>
<!-- Copied from https://htdp.org/2003-09-26/Solutions/speak.html by Cyotek WebCopy 1.8.0.652, 01 July 2020, 17:43:44 -->
</html>
