<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN" "DTD/xhtml1-strict.dtd">
<html>
<!-- Copied from https://htdp.org/2003-09-26/Solutions/permute-local.html by Cyotek WebCopy 1.8.0.652, 01 July 2020, 17:43:44 -->
<head>
<title>Exercise 18.1.9; How to Design Programs </title>
</head>
<body>
<a href="draw-poly-local.html">18.1.8</a> | <a href="../Book/curriculum-Z-H-23.html#node_thm_18.1.9">Problem Statement</a>
 | <a href="contents.html">Table of Contents</a>
 | <a href="blue-eye-local.html">18.1.10</a><pre>;; Language: Intermediate Student

#| 18.1.9 adapting arrangements
------------------------------------------------------------
 Permutations

   Data Definitions:
    A word is either
      * empty
      * (cons S W) where S is a (letter) symbol and W is a word. 

    A list-of-words is either
      * empty
      * (cons W L) where W is a word and L is a list-of-words.

    Examples:
      empty is a word
      (list 'b) is a word
      (list 'a 'b) is a word
      (list 'b 'a) is a word
     
      empty is a list of words
      (list (list 'a 'b)) is a list of words
      (list (list 'a 'b) (list 'b 'a)) is a list of words

arrangements : word -&gt; (listof word)
Purpose: compute all possible arrangements (permutations)
of all the letters in a a-word; the result is a list of
words  	
|#
(define (arrangements a-word)
  (local 
    [(define (arrangements a-word)
       (cond
         ((empty? a-word) (list empty))
         (else (insert-everywhere/all-words 
                (first a-word) 
                (arrangements (rest a-word))))))
     (define (insert-everywhere/all-words letter lo-words)
       (cond
         ((empty? lo-words) empty)
         (else 
          (append (insert-everywhere letter (first lo-words))
                  (insert-everywhere/all-words 
                   letter 
                   (rest lo-words))))))
     (define (insert-everywhere a-letter a-word)
       (cond
         ((empty? a-word) (list (list a-letter)))
         (else 
          (cons 
           (cons a-letter a-word) 
           (add-at-beginning 
            (first a-word)
            (insert-everywhere a-letter (rest a-word)))))))
     (define (add-at-beginning a-letter lo-words)
       (cond
         ((empty? lo-words) empty)
         (else 
          (cons 
           (cons a-letter (first lo-words))
           (add-at-beginning a-letter (rest lo-words))))))]
    (arrangements a-word)))

;Examples as Tests:
(check-expect
 (arrangements empty) 
 (list empty))

(check-expect
 (arrangements (list 'a))
 (list (list 'a)))

(check-expect
 (arrangements (list 'a 'b))
 (list (list 'a 'b)
       (list 'b 'a)))

(check-expect
 (arrangements (list 'a 'b 'c))
 (list (list 'a 'b 'c)
       (list 'b 'a 'c)
       (list 'b 'c 'a)
       (list 'a 'c 'b)
       (list 'c 'a 'b)
       (list 'c 'b 'a)))
</pre>
</body>
<!-- Copied from https://htdp.org/2003-09-26/Solutions/permute-local.html by Cyotek WebCopy 1.8.0.652, 01 July 2020, 17:43:44 -->
</html>
