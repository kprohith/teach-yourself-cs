<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN" "DTD/xhtml1-strict.dtd">
<html>
<!-- Copied from https://htdp.org/2003-09-26/Solutions/count-ft.html by Cyotek WebCopy 1.8.0.652, 01 July 2020, 17:43:44 -->
<head>
<title>Exercise 14.1.3; How to Design Programs </title>
</head>
<body>
<a href="bep-eval.html">14.1.2</a> | <a href="../Book/curriculum-Z-H-19.html#node_thm_14.1.3">Problem Statement</a>
 | <a href="contents.html">Table of Contents</a>
 | <a href="age-ft.html">14.1.4</a><pre>;; A family-tree-node (short: ftn) is either
;;
;;   1. empty; or
;;   2. (make-child f m na da ec)
;;      where f and m are ftns, na
;;      and ec are symbols, and da is a number.
(define-struct child (father mother name date eyes))

;; Oldest Generation:
(define Carl (make-child empty empty 'Carl 1926 'green))
(define Bettina (make-child empty empty 'Bettina 1926 'green))

;; Middle Generation: 
(define Adam (make-child Carl Bettina 'Adam 1950 'yellow))
(define Dave (make-child Carl Bettina 'Dave 1955 'black))
(define Eva (make-child Carl Bettina 'Eva 1965 'blue))
(define Fred (make-child empty empty 'Fred 1966 'pink))

;; Youngest Generation: 
(define Gustav (make-child Fred Eva 'Gustav 1988 'brown))

#|
;; TEMPLATE
;; fun-for-ftn : ftn -&gt; ???
(define (fun-for-ftn a-ftree)
  (cond [(empty? a-ftree) ...]
        [else ... (fun-for-ftn (child-father a-ftree)) ...
              ... (fun-for-ftn (child-mother a-ftree)) ...
              ... (child-name a-ftree) ...
              ... (child-date a-ftree) ...
              ... (child-eyes a-ftree) ...]))
|#

;; count-persons : ftn -&gt; number
;; to determine the number of people in a-ftree
(define (count-persons a-ftree) 
  (cond 
    [(empty? a-ftree) 0]
    [else (+ 1
             (count-persons (child-father a-ftree))
             (count-persons (child-mother a-ftree)))]))

;; EXAMPLES AS TESTS
(count-persons empty) "should be" 0
(count-persons Carl) "should be" 1
(count-persons Bettina) "should be" 1
(count-persons Adam) "should be" 3
(count-persons Dave) "should be" 3
(count-persons Eva) "should be" 3
(count-persons Fred) "should be" 1
(count-persons Gustav) "should be" 5
</pre>
</body>
<!-- Copied from https://htdp.org/2003-09-26/Solutions/count-ft.html by Cyotek WebCopy 1.8.0.652, 01 July 2020, 17:43:44 -->
</html>
