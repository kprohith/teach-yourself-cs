<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN" "DTD/xhtml1-strict.dtd">
<html>
<!-- Copied from https://htdp.org/2003-09-26/Solutions/list-5.html by Cyotek WebCopy 1.8.0.652, 01 July 2020, 17:43:44 -->
<head>
<title>Exercise 17.8.4; How to Design Programs </title>
</head>
<body>
<a href="list-1.html">17.8.3</a> | <a href="../Book/curriculum-Z-H-22.html#node_thm_17.8.4">Problem Statement</a>
 | <a href="contents.html">Table of Contents</a>
 | <a href="list-3.html">17.8.5</a><pre>;; Language: Beginning Student with List Abbreviations

#| 17.8.4 contains-same-numbers
------------------------------------------------------------
;Data Definition:
a list-of-numbers is either
  1. empty or
  2. (cons n lon)
  where n is a number and lon is a list-of-numbers

;Template:
(define (contains-same-numbers a-list another-list)
  (cond
    [(empty? a-list)...]
    [else
     ... (first a-list) ... (first another-list) ...
     ... (contains-same-numbers (rest a-list) 
                                (rest another-list)) ...]))
|#

;;contains-same-numbers : 
;;  list-of-numbers list-of-numbers  -&gt;  boolean
;;to determine whether a-list and another-list 
;;contain the same numbers. Numbers do not need 
;;to be in the same order. 
(define (contains-same-numbers a-list another-list)
  (and (all-in? a-list another-list)
       (all-in? another-list a-list)))

;Examples and Tests:
(check-expect
 (contains-same-numbers empty empty) 
 true)

(check-expect
 (contains-same-numbers empty (list 1)) 
 false)

(check-expect
 (contains-same-numbers (list 1) empty)
 false)

(check-expect
 (contains-same-numbers (list 1 2 3) 
                        (list 1 2 3)) 
 true)

(check-expect
 (contains-same-numbers (list 1 2 3) 
                        (list 1 3)) 
 false)

(check-expect
 (contains-same-numbers (list 1 2 3) 
                        (list 1 3 2)) 
 true)

(check-expect
 (contains-same-numbers (list 1 2 3) 
                        (list 3 1 2)) 
 true)

(check-expect
 (contains-same-numbers (list 1 1 1 1 1) 
                        (list 1)) 
 true)

(check-expect
 (contains-same-numbers (list 1) 
                        (list 1 1 1 1 1)) 
 true)

(check-expect
 (contains-same-numbers (list 1 2 3)
                        (list 1 3 2 2))
 true)



#|We need a helper function that identifies if 
all the numbers in a list are in another list
------------------------------------------------------------
;Template:
(define (all-in? a-lon another-lon)
  (cond
    [(empty? a-lon)...]
    [else ...(first a-lon)...
          ...(all-in? (rest a-lon) another-lon)...]))
|#

;;all-in?: list-of-numbers list-of-numbers -&gt; boolean
;;consumes a list-of-numbers and another list-of-numbers and returns
;;true if all the numbers in the first list are also in the second list
(define (all-in? a-lon another-lon)
  (cond
    [(empty? a-lon) true]
    [else 
     (and (one-in? (first a-lon) another-lon)
          (all-in? (rest a-lon) another-lon))]))

;Examples and Tests:
(check-expect
 (all-in? empty empty)
 true)

(check-expect
 (all-in? (list 5) (list 5))
 true)

(check-expect
 (all-in? (list 5 8 7) (list 8 7 5))
 true)

(check-expect
 (all-in? (list 5 4) (list 3 4 5))
 true)

(check-expect
 (all-in? (list 3 4 5) (list 5 4))
 false)

#|We need a helper function that identifies if 
a number is in a list
------------------------------------------------------------
;Template:
(define (one-in? n a-lon)
  (cond
    [(empty? a-lon)...]
    [else ...(first a-lon)...
          ...(one-in? n (rest a-lon)]...]))
|#
;;one-in?: number list-of-numbers -&gt; boolean 
;;consumes a number and a list-of-numbers and returns true 
;;if the number is in the list, false otherwise  
(define (one-in? num a-lon)
  (cond
    [(empty? a-lon) false]
    [else
     (or (= num (first a-lon))
         (one-in? num (rest a-lon)))]))

;Examples and Tests:
(check-expect
 (one-in? 5 empty)
 false)

(check-expect
 (one-in? 5 (list 5))
 true)

(check-expect
 (one-in? 5 (list 5 7 4))
 true)

(check-expect
 (one-in? 5 (list 4 7 5 2))
 true)

(check-expect
 (one-in? 5 (list 4 3 7 9 2))
 false)

</pre>
</body>
<!-- Copied from https://htdp.org/2003-09-26/Solutions/list-5.html by Cyotek WebCopy 1.8.0.652, 01 July 2020, 17:43:44 -->
</html>
